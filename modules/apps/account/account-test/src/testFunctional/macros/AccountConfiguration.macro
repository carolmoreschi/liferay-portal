definition {

	macro addAccountManager {
		JSONUser.addUser(
			userEmailAddress = "${userEmailAddress}",
			userFirstName = "${userFirstName}",
			userLastName = "${userLastName}",
			userScreenName = "${userScreenName}");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "${userEmailAddress}");

		JSONOrganization.addOrganization(organizationName = "${orgName}");

		JSONOrganization.assignUserToOrganization(
			organizationName = "${orgName}",
			userEmailAddress = "${userEmailAddress}");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignOrgRoleCP(
			roleTitle = "${orgRoleTitle}",
			userScreenName = "${userScreenName}");

		ApplicationsMenu.gotoPortlet(
			category = "Accounts",
			panel = "Applications",
			portlet = "Accounts");

		LexiconEntry.gotoEntry(rowEntry = "${accountName}");

		Accounts.gotoAssigneeTab(assigneeTab = "Organizations");

		Accounts.addAssignee(
			accountName = "${accountName}",
			assigneeName = "${orgName}",
			assigneeType = "Organizations");
	}

	macro addBlockedEmailDomain {
		PortalSettings.gotoConfiguration(
			configurationCategory = "Accounts",
			configurationName = "Email Domains",
			configurationScope = "Virtual Instance Scope");

		for (var settingValue : list "${settingValues}") {
			Type.sendKeys(
				key_label = "Blocked Email Domains",
				locator1 = "TextArea#ANY_LABELED",
				value1 = "${settingValue}");

			KeyPress(
				key_label = "Blocked Email Domains",
				locator1 = "TextArea#ANY_LABELED",
				value1 = "\RETURN");
		}

		if (IsElementPresent(locator1 = "Button#UPDATE")) {
			PortletEntry.update();
		}

		else if (IsElementPresent(locator1 = "Button#SAVE")) {
			PortletEntry.save();
		}
	}

	macro enableAccountEmailDomain {
		PortalSettings.gotoConfiguration(
			configurationCategory = "Accounts",
			configurationName = "Email Domains",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.editConfigurationUsersPersonalMenuShowInControlMenu(toggleSwitchLabel = "Enable Email Domain Validation");
	}

}